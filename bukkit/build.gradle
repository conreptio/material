ext {
    bukkit = project
    util = project('natrolite-utils')

    POM_NAME = 'Natrolite Bukkit'
    POM_LICENSE_NAME = 'GNU General Public License (GPL)'
    POM_LICENSE_URL = 'https://www.gnu.org/licenses/gpl.txt'
    POM_LICENSE_DIST = 'repo'

    utils = new Properties()
    bukkitVersion = '1.11.2-R0.1-SNAPSHOT'
}

utils.with {
    core = this.util.project('core')
    spiget = this.util.project('spiget')
    spigetClient = this.util.project('spiget-client')
}

subprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'net.minecrell.licenser'

    group += '.bukkit'

    checkstyle {
        toolVersion = '7.6.1'
        configFile = bukkit.file('checkstyle.xml')
    }

    task checkstyle(dependsOn: tasks.withType(Checkstyle))

    gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
        if (!graph.allTasks.contains(tasks.checkstyle)) {
            graph.allTasks.findAll { it.name.startsWith('checkstyle') }.each { it.enabled = false }
        }
    }

    license {
        header = bukkit.file('HEADER.txt')
        include '**/*.java'
        newLine = true
    }

    plugins.withType(JavaPlugin) {
        repositories {
            maven {
                url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            }
            maven {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            }
        }

        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        dependencies {
            compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '1.3.9'
        }
    }
}
