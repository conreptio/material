ext {
    bukkit = project
    util = project('natrolite-utils')

    licenseName = 'GNU General Public License (GPL)'
    licenseUrl = 'https://www.gnu.org/licenses/gpl.txt'
    licenseDist = 'repo'

    utils = new Properties()
    bukkitVersion = '1.11.2-R0.1-SNAPSHOT'
}

utils.putAll(util.subprojects.collectEntries {
    project -> [project.name, project]
})

subprojects {
    group += '.bukkit'

    plugins.withType(JavaPlugin) {

        plugins.apply('checkstyle')
        plugins.apply('com.github.johnrengelman.shadow')
        plugins.apply('net.minecrell.licenser')

        repositories {
            maven {
                url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            }
            maven {
                url 'https://oss.sonatype.org/content/repositories/snapshots/'
            }
        }

        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'

        dependencies {
            compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: '1.3.9'
        }

        processResources {
            from bukkit.file('LICENSE.txt')
        }

        checkstyle {
            toolVersion = '7.6.1'
            configFile = bukkit.file('checkstyle.xml')
        }

        task checkstyle(dependsOn: tasks.withType(Checkstyle))

        gradle.taskGraph.whenReady { TaskExecutionGraph graph ->
            if (!graph.allTasks.contains(tasks.checkstyle)) {
                graph.allTasks.findAll {
                    it.name.startsWith('checkstyle')
                }.each {
                    it.enabled = false
                }
            }
        }

        license {
            header = bukkit.file('HEADER.txt')
            include '**/*.java'
            newLine = true
        }
    }
}
