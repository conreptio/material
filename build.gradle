plugins {
    id 'net.minecrell.licenser' version '0.3' apply false
    id 'com.github.johnrengelman.shadow' version '1.2.4' apply false
}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/eclipse.gradle'

description = 'Natrolite Minigame Manager'

ext {
    homepage = 'https://natrolite.org/'

    issueSystem = 'BitBucket Issues'
    issueUrl = 'https://bitbucket.org/conreptio/natrolite/issues'

    ciSystem = 'BitBucket Pipelines'
    ciUrl = 'https://bitbucket.org/conreptio/natrolite/addon/pipelines/home'

    scmUrl = 'https://bitbucket.org/conreptio/natrolite/src'
    scmConnection = 'scm:git:git://bitbucket.org:conreptio/natrolite.git'
    scmDevConnection = 'scm:git:ssh://bitbucket.org:conreptio/natrolite.git'
}

allprojects {
    group = 'org.natrolite'

    ext {
        javaVersion = '1.8'
        encoding = 'UTF-8'
    }

    repositories {
        mavenCentral()
        maven { url 'https://bitbucket.org/conreptio/maven-public/raw/master/releases' }
        maven { url 'https://bitbucket.org/conreptio/maven-public/raw/master/snapshots' }
    }
}

subprojects { sub ->
    version = rootProject.version

    plugins.withType(JavaPlugin) {
        plugins.apply('net.minecrell.licenser')

        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion

        license {
            header = rootProject.file('HEADER.txt')
            include '**/*.java'
            newLine = true
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}